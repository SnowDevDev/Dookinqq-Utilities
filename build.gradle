plugins {
    id "fabric-loom" version "1.8-SNAPSHOT"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url = "https://maven.meteordev.org/releases" }
    maven { url = "https://maven.meteordev.org/snapshots" }
}

dependencies {
    // Minecraft & Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Meteor (matches MC version automatically)
    modImplementation "meteordevelopment:meteor-client:${project.minecraft_version}-SNAPSHOT"

    // Extra
    implementation "com.github.luben:zstd-jni:1.5.5-11"

    //Renderer
    include modImplementation("io.github.0x3c50.renderer:renderer-fabric:1.2.5")
}

processResources {
    inputs.property "version", project.version
    inputs.property "mc_version", project.minecraft_version

    filesMatching("fabric.mod.json") {
        expand(
            version: project.version,
            mc_version: project.minecraft_version
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}